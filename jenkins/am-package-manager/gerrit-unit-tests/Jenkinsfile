#!/usr/bin/env groovy

@Library('jenkins-shared-libs')
import static com.ericsson.orchestration.mgmt.libs.VnfmJenkins.getAgentLabel
import static com.ericsson.orchestration.mgmt.libs.VnfmJenkins.getSeedJobName


// Stages template
def stages = [:]
// Set Agent label
env.SEED_JOB = getSeedJobName(JOB_NAME)
String label = getAgentLabel( type: 'unit-tests',
                              project: env.SEED_JOB,
                              this)


node(label) {
    try {
        // Set Job's ENVs
        env.JOB_TYPE = 'gerrit'
        env.GLOBAL_VAR_PATH = 'jenkins/global.var'
        env.VAR_PATH = 'jenkins/' + env.SEED_JOB + '/project.var'
        env.GERRIT_BRANCH = params.GERRIT_BRANCH ?: 'master'
        env.GERRIT_REFSPEC = params.GERRIT_REFSPEC ?: 'refs/heads/master'


        // Run Prepare Stage
        stage('Prepare') {
            println('INFO: Checkout CI code...')
            checkout(scm)

            println('INFO: Load Global vars...')
            load(env.GLOBAL_VAR_PATH)

            println('INFO: Load Project vars...')
            load(env.VAR_PATH)

            println('INFO: Load Stages...')
            stages['git'] = load(env.GIT_STAGE)
            stages['envs'] = load(env.ENVS_STAGE)
            stages['build'] = load(env.BUILD_STAGE)
            stages['unit-tests'] = load(UNIT_TESTS_STAGE)
            stages['docker'] = load(env.DOCKER_STAGE)
            stages['ua-tests'] = load(env.UA_TESTS_STAGE)
            stages['clean'] = load(env.CLEAN_STAGE)
        }

        // Run Checkout Project Stage
        stages['git'].CheckoutProject(project: env.GERRIT_PROJECT,
                                      branch: env.GERRIT_BRANCH,
                                      refspec: env.GERRIT_REFSPEC,
                                      type: 'change')

        // Run Update Job ENVs Stage
        stages['envs'].Update(project: env.GERRIT_PROJECT)

        // Run Update POM files Stage
        stages['build'].UpdatePOMProperty(project: env.GERRIT_PROJECT,
                                          property: 'all',
                                          value: env.PROJECT_VERSION.split('-')[0])

        // Run Build Stage
        stages['build'].BuildMaven(project: env.GERRIT_PROJECT)

        // Run Unit Tests Stage
        stages['unit-tests'].MavenUnitTests(project: env.GERRIT_PROJECT)

        // Run Build Image Stage
        stages['docker'].BuildImage(name: env.GERRIT_PROJECT)

        // Run Acceptance Tests Stage
        stages['ua-tests'].MavenCSAR(name: env.GERRIT_PROJECT)

    } catch (Exception errJob) {
        println(errJob)
        currentBuild.result = 'FAILURE'
    } finally {
        // Run Clean Stage
        stages['clean'].CleanBuild()
    }
}