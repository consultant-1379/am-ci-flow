#!/usr/bin/env groovy

@Library('jenkins-shared-libs')
import static Boolean.parseBoolean
import static com.ericsson.orchestration.mgmt.libs.VnfmJenkins.getAgentLabel
import static com.ericsson.orchestration.mgmt.libs.VnfmJenkins.getSeedJobName


// Stages template
def stages = [:]
// Set Agent label
String label = getAgentLabel( type: 'release',
                              this)


node(label) {
    try {
        // Set Job's ENVs
        env.SEED_JOB = getSeedJobName(JOB_NAME)
        env.JOB_TYPE = 'release'
        env.GLOBAL_VAR_PATH = 'jenkins/global.var'
        env.VAR_PATH = 'jenkins/' + env.SEED_JOB + '/project.var'
        env.BRANCH = params.BRANCH
        env.TYPE = params.TYPE
        env.GERRIT_CHANGE_SUBJECT = params.GERRIT_CHANGE_SUBJECT


        // Run Prepare Stage
        stage('Prepare') {
            println('INFO: Checkout CI code...')
            checkout(scm)

            println('INFO: Load Global vars...')
            load(env.GLOBAL_VAR_PATH)

            println('INFO: Load Project vars...')
            load(env.VAR_PATH)

            println('INFO: Load Stages...')
            stages['git'] = load(env.GIT_STAGE)
            stages['envs'] = load(env.ENVS_STAGE)
            stages['helm'] = load(env.HELM_STAGE)
            stages['post'] = load(env.POST_STAGE)
            stages['jira'] = load(env.JIRA_STAGE)
            stages['clean'] = load(env.CLEAN_STAGE)
            // Load Build stage template
            if(!parseBoolean(env.SKIP_BUILD)) {
                stages['build'] = load(env.BUILD_STAGE)
            }
            // Load Docker stage template
            if(!parseBoolean(env.SKIP_BUILD_IMAGE_TEST)) {
                stages['docker'] = load(env.DOCKER_STAGE)
            }
        }

        // Run Checkout Project Stage
        stages['git'].CheckoutProject(project: env.GERRIT_PROJECT,
                                      branch: env.BRANCH)

        // Run Update Job ENVs Stage
        stages['envs'].Update(project: env.GERRIT_PROJECT,
                              type: env.TYPE)

        if(!parseBoolean(env.SKIP_BUILD)) {
            // Run Build Stage
            stages['build'].BuildMaven(project: env.GERRIT_PROJECT)
        }

        if(!parseBoolean(env.SKIP_BUILD_IMAGE_TEST)) {
            // Run Build Image Stage
            stages['docker'].BuildImage(name: env.GERRIT_PROJECT)
        }

        // Run Package Helm Chart Stage
        stages['helm'].PackageChart(project: env.GERRIT_PROJECT,
                                    version: env.CHART_VERSION,
                                    upload: true)

        // Run Git Commit Release Stage
        stages['git'].CommitRelease(project: env.GERRIT_PROJECT,
                                    version: env.CHART_VERSION)

        // Run Push Changes Stage
        stages['git'].PushChanges(project: env.GERRIT_PROJECT,
                                  branch: env.BRANCH,
                                  version: env.CHART_VERSION)

    } catch (Exception errJob) {
        println(errJob)
        currentBuild.result = 'FAILURE'
    } finally {
        // Run Post Actions Stage
        stages['post'].Release(status: currentBuild.result ?: 'SUCCESS')

        // Run Add Comment to Jira Ticket Stage
        stages['jira'].AddJiraTicketComment(url: env.JIRA_URL,
                                            subject: env.GERRIT_CHANGE_SUBJECT)

        // Run Clean Stage
        stages['clean'].CleanBuild()
    }
}